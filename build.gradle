plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.jfrog.artifactory' version '4.24.14'
    id 'org.sonarqube' version '3.3'
    id 'org.owasp.dependencycheck' version '6.1.6'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'com.gorylenko.gradle-git-properties' version '2.3.1'
}

artifactoryPublish.skip = true

group = 'fi.vm.yti'
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenLocal()
    mavenCentral {
        content { excludeGroupByRegex "fi\\.vm\\.yti.*" }
    }
    jcenter {
        content { excludeGroupByRegex "fi\\.vm\\.yti.*" }
    }

    // put your artifactory parameters to $HOME/.gradle/gradle.properties
    if (project.hasProperty('artifactoryUrl')) {
        maven {
            url "$artifactoryUrl"
            credentials {
                username "$artifactoryUsername"
                password "$artifactoryPassword"
            }
        }
    }
    maven {
        url 'https://repo.spring.io/milestone'
        content { excludeGroupByRegex "fi\\.vm\\.yti.*" }
    }
}

ext {
    set('springCloudVersion', "2020.0.3")
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
        cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-activemq'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'

    implementation 'org.springframework.security:spring-security-core'
    implementation 'org.springframework.security:spring-security-config'
    implementation 'org.springframework.security:spring-security-web'

    implementation 'org.springdoc:springdoc-openapi-ui:1.5.9'

    implementation 'fi.vm.yti:yti-spring-security:0.1.17'
    implementation 'fi.vm.yti:yti-spring-migration:0.1.4'

    implementation 'org.apache.activemq:activemq-broker'
    implementation 'org.elasticsearch:elasticsearch:6.5.4'
    implementation 'org.elasticsearch.client:elasticsearch-rest-client:6.5.4'
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:6.5.4'
    implementation 'ch.qos.logback:logback-parent:1.2.3'
    implementation 'ch.qos.logback:logback-core'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.6'
    implementation 'org.jetbrains:annotations:21.0.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.jsoup:jsoup:1.14.1'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:3.0.1'
    implementation 'com.sun.xml.bind:jaxb-impl:3.0.2'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'jakarta.activation:jakarta.activation-api:2.0.1'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'junit:junit:4.13.2'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

sonarqube {
    properties {
        property "sonar.projectKey", "yti-terminology-api"
        property "sonar.projectName", "yti-terminology-api"
        property("sonar.dependencyCheck.reportPath", "$buildDir/reports/dependency-check-report.xml")
        property("sonar.dependencyCheck.htmlReportPath", "$buildDir/reports/dependency-check-report.html")
    }
}

dependencyCheck {
    format = 'ALL'
    autoUpdate = false
    analyzers {
        centralEnabled = false
    }
    data {
        directory = System.getenv('OWASP_DATA_DIR')
    }
}
